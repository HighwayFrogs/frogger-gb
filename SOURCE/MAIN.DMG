;********************
;*** Main Program ***
;********************


main
	ld	a,(IE)
	set	4,a		;Enable Keyboard interrupt (for emulator)
	ld      (IE),a		;Interrupt Enable
	ld	a,(frame)
	bit	0,a
	jr	z,nork		;Don't read keys on even field. Allows (trg1) to function
	call	read_keys	;Read keys (Do this with emulator interrupt enabled to prevent false keypresses)
nork


	call	coldstart	;test for four-button restart

	if	audio
	call	gmusic
	call	gsound

	if devel

	ld	a,(frame)
	and	1
	jr	nz,nosound2
	ld	a,(trg1)
	and	akey
	jr	z,nosound0
	ld	a,(audtest)
	or	$80
	call	gsetsnd
nosound0
	ld	a,(trg1)
	and	bkey
	jr	z,nosound2
	ld	a,(audtest)
	inc	a
	cp	$f
	jr	c,testok
	ld	a,$0
testok	ld	(audtest),a
	ld	(score),a
	call	drawscore
nosound2

	endif


	endif


	if	prodrom
	else
	ld	a,(trg1)
	cp	akey+bkey
	jr	nz,nocheat
	ld	a,$80
	ld	(menleft),a
	call drawmen
nocheat
	endif


; game calls go here

;	if	wholegame

	ld	a,(mode)
	and	a
	jr	nz,gamemode
	call	piclogic
	ld	a,(frame)
	and	1
	jr	z,freeze
	ld	a,(titleflag)
	and	a
	jr	nz,freeze
	inc	a
	ld	(titleflag),a

	if 	audio
	ld	a,title_s
	call	gsetseq
	endif

	jp 	freeze		;skip game logic during title
gamemode
;	endif

	ld	a,(mode)
	cp	gmode
	jr	z,dogame
	cp	omode		;game over?
	jr	nz,nfreeze	;something else (not yet implemented)

				;check for start key
	call	govercode
	jr	freeze
nfreeze
	cp	smode
	jr	nz,nfreeze1
	call	selcode
	jr	freeze

nfreeze1	      		;space for other modes
	cp	rmode0
	jr	nz,nfreeze2
	call	readycode
	jr	freeze
nfreeze2
	cp	rmode1
	jr	nz,nfreeze3
	call	readycode
	jr	freeze
nfreeze3
	cp	bmode	;between levels?
	jr	nz,nfreeze4
	call	tweencode
	jr	freeze
nfreeze4
	cp lmode
	call	z,logocode
	jr	freeze

dogame

	ld	a,(pause)
	and	a
	jr	nz,freeze


	call 	collisions	;
	call	xscroll		;scroll lanes
	call	drawfrog	;
	call	jumping		;see if jumping, handle jump
	call	checkstatus	;see if frog is dying, or reached home
	call	gamelogic
freeze

vbwtlp	halt			;Stop processor clock until interrupt
	ld      a,(vbflag)	;Wait for v-blank
	and     a
	jr      z,vbwtlp



;Vertical blank processing

	call vblank


	ld	hl,frame	;Increment 2 byte frame count
	inc	(hl)


; calls made during vblank (after interrupt) go here

; game calls

	ld	a,(mode)
	cp	gmode
	jr	nz,skipscore

	ld	a,(pause)
	and	a
	jr	nz,freeze1

	call	turtles
	call	drawscore

freeze1

	call	dopause		;do pause mode (and game start)
	jp	nz,endmain1

skipscore
	ld	a,(mode)
	cp	omode
	call	z,gvbcode

	ld	a,(NR51)
	and	a
	jr	nz,nrest51
	ld	a,(soundsav)	;restore sound mix
	ld     (NR51),a
nrest51

endmain1

	jp	main


coldstart
	ld	a,(cnt1)
	cp	$0f
	ret	nz
	xor	a
	ld	(gameover),a
	ld	(titleflag),a
	if audio
	call	gkillsnd
	endif
	jp 	g1
